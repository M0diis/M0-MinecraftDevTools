plugins {
    id 'fabric-loom' version '1.9-SNAPSHOT'
    id 'maven-publish'
}

version = project.mod_version
group = project.maven_group

base {
    archivesName = project.archives_base_name
}

repositories {
    mavenCentral()

    maven {
        name = 'Modrinth'
        url = 'https://api.modrinth.com/maven'
    }

    maven {
        url = 'https://maven.wispforest.io/releases/'
    }

    maven {
        url = "https://maven.shedaniel.me/"
    }

    maven {
        url = "https://maven.terraformersmc.com/releases/"
    }

    exclusiveContent {
        forRepository {
            maven {
                name = "Modrinth"
                url = "https://api.modrinth.com/maven"
            }
        }
        filter {
            includeGroup "maven.modrinth"
        }
    }
}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin since it *excludes* files.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
    modImplementation "maven.modrinth:midnightlib:1.7.3+1.21.4-fabric"
    modImplementation("me.shedaniel.cloth:cloth-config-fabric:17.0.144") {
        exclude(group: "net.fabricmc.fabric-api")
    }
    modImplementation "io.wispforest:owo-lib:${project.owo_version}"
    include "io.wispforest:owo-sentinel:${project.owo_version}"

    compileOnly "org.projectlombok:lombok:${project.lombok_version}"
    annotationProcessor "org.projectlombok:lombok:${project.lombok_version}"

    modImplementation "maven.modrinth:nbt-autocomplete:nJhOBbir"

    modImplementation 'org.codehaus.groovy:groovy:3.0.21'
    include 'org.codehaus.groovy:groovy:3.0.21'

    modImplementation("org.jetbrains.kotlin:kotlin-scripting-jsr223:1.9.22")
    modImplementation("eu.jrie.jetbrains:kotlin-shell-core:0.2.1")

    include "org.jetbrains.kotlin:kotlin-scripting-jsr223:1.9.22"
    include "eu.jrie.jetbrains:kotlin-shell-core:0.2.1"

    modImplementation "org.jetbrains.kotlin:kotlin-compiler-embeddable:1.9.22"
    modImplementation "org.jetbrains.kotlin:kotlin-scripting-compiler-embeddable:1.9.22"
    include "org.jetbrains.kotlin:kotlin-compiler-embeddable:1.9.22"
    include "org.jetbrains.kotlin:kotlin-scripting-compiler-embeddable:1.9.22"
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

loom {
    runs {
        client {
            programArgs "--username", "M0dii"
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 21
}

java {
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    withSourcesJar()

    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.base.archivesName.get()}" }
    }
}

loom {
    accessWidenerPath = file("src/main/resources/m0dev.accesswidener")
}

publishing {
    publications {
        create("mavenJava", MavenPublication) {
            artifactId = project.archives_base_name
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
        // Notice: This block does NOT have the same function as the block in the top level.
        // The repositories here will be used for publishing your artifact, not for
        // retrieving dependencies.
    }
}